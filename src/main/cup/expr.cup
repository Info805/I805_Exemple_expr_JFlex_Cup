/* package et imports */
package fr.usmb.demo.m1.expr;
import java_cup.runtime.Symbol;

// nom de la classe a generer
class Parser;

/* inclusion de code */
action code {:
    // pour utilisation dans les actions (classe action)
:};

parser code {:
    // pour le parser (exeple redefinition methode report_error,...)
    // pour le parser (redefinition de la methode reportant les erreurs d'analyse)
    public void report_error(String message, Object info) {
        String m = "";
        if (info instanceof java_cup.runtime.Symbol) {
            Symbol s = ((Symbol) info);
            if (s != null && s.left >= 0 ) {
                /* Ajoute le numero de ligne  et de colonne*/
                m =  "Ligne " + (s.left+1) + ", Colonne " + (s.right+1) + " : ";
                }
            }
        m = m + message;
        System.err.println(m);
        }
:};

init with {:
    // initialisation du parser
:};

/* declarations (terminaux, non-terminaux, etc.) */
/* symboles terminaux */
terminal PLUS, MUL, PG, PD, SEMI;
terminal Integer ENTIER;

/* non terminaux */
non terminal listeexpr;
non terminal Integer expr;

precedence left PLUS;
precedence left MUL;

/* grammaire */

listeexpr   ::= listeexpr expr:e {: System.out.println("valeur = "+e); :} SEMI
              | expr:e {: System.out.println("valeur = "+e); :} SEMI
              | error SEMI
              ;
expr  ::= expr:e1  PLUS expr:e2   {: RESULT = e1 + e2; :}
        | expr:e1 MUL expr:e2     {: RESULT = e1 * e2 ;:}
        | PG expr:e PD      {: RESULT = e; :}
        | ENTIER:e          {: RESULT = e; :}
        ;
